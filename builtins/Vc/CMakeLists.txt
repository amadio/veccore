include(ExternalProject)

set(Vc_VERSION "1.4.2")
set(Vc_PROJECT "Vc-${Vc_VERSION}")
set(Vc_SRC_SHA "SHA256=50d3f151e40b0718666935aa71d299d6370fafa67411f0a9e249fbce3e6e3952")
set(Vc_DESTDIR "${CMAKE_BINARY_DIR}/${Vc_PROJECT}")
set(Vc_ROOTDIR "${Vc_DESTDIR}/${CMAKE_INSTALL_PREFIX}")

set(Vc_LIBNAME ${CMAKE_STATIC_LIBRARY_PREFIX}Vc${CMAKE_STATIC_LIBRARY_SUFFIX})
set(Vc_LIBRARY ${Vc_ROOTDIR}/lib${LIB_SUFFIX}/${Vc_LIBNAME})

ExternalProject_Add(${Vc_PROJECT}
  URL "http://lcgpackages.web.cern.ch/lcgpackages/tarFiles/sources/${Vc_PROJECT}.tar.gz"
  URL_HASH ${Vc_SRC_SHA}
  BUILD_IN_SOURCE 0
  BUILD_BYPRODUCTS ${Vc_LIBRARY}
  CMAKE_ARGS -G ${CMAKE_GENERATOR}
             -DCMAKE_BUILD_TYPE=${CMAKE_BUILD_TYPE}
             -DCMAKE_C_COMPILER=${CMAKE_C_COMPILER}
             -DCMAKE_C_FLAGS=${CMAKE_C_FLAGS}
             -DCMAKE_CXX_COMPILER=${CMAKE_CXX_COMPILER}
             -DCMAKE_CXX_FLAGS=-w\ ${CMAKE_CXX_FLAGS}
             -DCMAKE_INSTALL_PREFIX=${CMAKE_INSTALL_PREFIX}
  INSTALL_COMMAND env DESTDIR=${Vc_DESTDIR} ${CMAKE_COMMAND} --build . --target install
)

set(Vc_LIBRARY ${Vc_LIBRARY} CACHE INTERNAL "" FORCE)
set(Vc_LIBRARIES ${Vc_LIBRARY} CACHE INTERNAL "" FORCE)
set(Vc_INCLUDE_DIR "${Vc_ROOTDIR}/include" CACHE INTERNAL "" FORCE)
set(Vc_INCLUDE_DIRS "${Vc_ROOTDIR}/include" CACHE INTERNAL "" FORCE)

add_library(Vc STATIC IMPORTED)
set_property(TARGET Vc PROPERTY IMPORTED_LOCATION ${Vc_LIBRARY})
set_property(TARGET Vc PROPERTY INTERFACE_INCLUDE_DIRECTORIES $<BUILD_INTERFACE:${Vc_ROOTDIR}/include>)

add_dependencies(Vc ${Vc_PROJECT})
add_dependencies(VecCore ${Vc_PROJECT})

include(FindPackageHandleStandardArgs)
find_package_handle_standard_args(Vc
  REQUIRED_VARS Vc_INCLUDE_DIRS Vc_LIBRARIES VERSION_VAR Vc_VERSION)

install(DIRECTORY ${Vc_ROOTDIR}/ DESTINATION ".")
